plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.7.5'
    id "com.diffplug.spotless" version "5.1.1"
    id "com.palantir.docker" version "0.22.1"
}

group = 'uk.ac.ebi.ena.webin-cli'
version = '8.1.1'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven"

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = gitlab_private_token
        }
        authentication {
            header( HttpHeaderAuthentication )
        }
    }
    mavenCentral()
}

dependencies {
    // dependencies declared as 'api' so they can be exposed to projects that use webin-cli as a library.
    api( 'uk.ac.ebi.ena.sequence:sequencetools:2.+' )
    api( 'uk.ac.ebi.ena.txmbtools:txmbtools:2.+' )
    api( 'uk.ac.ebi.ena:readtools:2.+' ) {
        exclude module: "commons-logging"
    }
    implementation( 'uk.ac.ebi.ena.sra:webin-xml:2.+' )

    implementation( 'ch.qos.logback:logback-classic:1.3.14' )

    implementation( 'org.springframework:spring-web:6.1.13' )
    implementation( 'org.springframework.retry:spring-retry:1.3.4' )

    implementation( 'info.picocli:picocli:4.6.3' )

    implementation( 'org.fusesource.jansi:jansi:1.18' )

    implementation( 'de.vandermeer:asciitable:0.3.2' )

    implementation( "org.jdom:jdom:2.0.2" )

    implementation 'commons-lang:commons-lang:2.6'
    implementation( "commons-net:commons-net:3.10.0" )
    implementation( 'org.apache.commons:commons-compress:1.25.0')

    implementation( 'org.apache.poi:poi:5.2.1' )
    implementation( 'org.apache.poi:poi-ooxml:5.2.1' )

    implementation( 'com.fasterxml.jackson.core:jackson-databind:2.17.2' )
    runtimeOnly( 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2' )

    testImplementation( 'junit:junit:4.13' )
    testImplementation( 'org.assertj:assertj-core:3.11.1' )
    testImplementation( 'org.mockito:mockito-core:2.23.4' )
    testImplementation( 'com.google.guava:guava:33.3.1-jre' )

    configurations.all {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    jvmArgs "-Dfile.encoding=UTF-8"
    maxHeapSize = '4G'

    if( System.env.webin_cli_password ) {
        environment 'webin-cli-password', System.env.webin_cli_password
    } else {
        environment 'webin-cli-password', webinCliPassword
    }

    if( System.env.webin_cli_username ) {
        environment 'webin-cli-username', System.env.webin_cli_username
    } else {
        environment 'webin-cli-username', webinCliUsername
    }
}

spotless {
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        licenseHeader('''/*
 * Copyright 2018-2023 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
''')
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
        googleJavaFormat()
    }
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date()
    }

    mainClass = 'uk.ac.ebi.ena.webin.cli.WebinCli'
}

jar {
    manifest {
        attributes.putAll(bootJar.manifest.attributes)
    }

    exclude('logback.xml')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.name
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/811/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header( HttpHeaderAuthentication )
            }
        }
    }
}

// Build and deploy docker image:
// gradle docker
// gradle dockerTag
// gradle dockerTagsPush
docker {
    dependsOn build
    def repository = "enasequence/webin-cli"
    name "${repository}:${version}"
    tag "-latest" , "${repository}:latest"
    tag "-${version}" , "${repository}:${version}"
    files bootJar.archiveFile
    buildArgs(['JAR_FILE': "webin-cli-${version}.jar"])
}
