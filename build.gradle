plugins {
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id "io.github.ddimtirov.codacy" version "0.1.0"
    id "com.diffplug.spotless" version "5.1.1"
    id "com.palantir.docker" version "0.22.1"
    id 'java'
    id 'maven-publish'
    id 'idea'
}

apply plugin: 'io.spring.dependency-management'

group = 'uk.ac.ebi.ena.webin-cli'
version = '3.0.2'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

spotless {
    java {
        licenseHeader('''/*
 * Copyright 2018-2019 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
''')
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
    }
}

bootJar {
    mainClassName = 'uk.ac.ebi.ena.webin.cli.WebinCli'
    excludeDevtools = true
   	manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date()
    }
}

test {
	jvmArgs "-Dfile.encoding=UTF-8"
	maxHeapSize = '4G'

	if( System.env.webin_cli_password ) {
      	environment 'webin-cli-password', System.env.webin_cli_password
    } else {
    	environment 'webin-cli-password', webinCliPassword
    }

	if( System.env.webin_cli_username ) {
      	environment 'webin-cli-username', System.env.webin_cli_username
    } else {
    	environment 'webin-cli-username', webinCliUsername
    }
}

repositories {
    mavenLocal()
    maven { url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven" }
    mavenCentral()
}

task printWebinTestCredentials {
    doLast {
        println 'webinCliUsername: ' + webinCliUsername
        println 'webinCliPassword: ' + webinCliPassword
    }
}

dependencies {
    implementation( 'uk.ac.ebi.ena.webin-cli:webin-cli-validator:1.0.9' )
    implementation( 'uk.ac.ebi.ena.sequence:sequencetools:2.0.75' )
    implementation( 'uk.ac.ebi.ena.txmbtools:txmbtools:1.0.2' )
    implementation( 'uk.ac.ebi.ena:readtools:1.2.0' )
    implementation( 'org.springframework.boot:spring-boot-starter' )
    implementation( 'org.springframework.boot:spring-boot-starter-web' ) {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation( "commons-net:commons-net:3.4" )
    implementation( 'info.picocli:picocli:3.9.1' )
    implementation( 'org.fusesource.jansi:jansi:1.18' )
    implementation( 'de.vandermeer:asciitable:0.3.2' )
    implementation( "org.jdom:jdom:2.0.2" )
    // implementation('com.opencsv:opencsv:4.5')
    implementation( 'org.apache.poi:poi:4.0.1' )
    implementation( 'org.apache.poi:poi-ooxml:4.0.1' )

    testImplementation( 'org.springframework.boot:spring-boot-starter-test' )
    testImplementation( 'org.assertj:assertj-core:3.11.1' )
    testImplementation( 'org.mockito:mockito-core:2.23.4' )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
        	artifactId project.name
            from components.java
            artifact sourceJar
        }
    }
    repositories {
	    maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/811/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = gitlab_private_token // .gradle/gradle.properties
            }
            authentication {
                header( HttpHeaderAuthentication )
            }
	    }
    }
}

// Build and deploy docker image:
// gradle docker
// gradle dockerTag
// gradle dockerTagsPush

docker {
    dependsOn build
    def repository = "enasequence/webin-cli"
    name "${repository}:${version}"
    tag "-latest" , "${repository}:latest"
    tag "-${version}" , "${repository}:${version}"
    files bootJar.archiveFile
    buildArgs(['JAR_FILE': "webin-cli-${version}.jar"])
}
